plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.10'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.diffplug.spotless' version '6.18.0' apply false
    id 'jacoco'
}

group = 'org.mifos'
version = '0.2.0'
description = 'ph-ee-connector-ams-fineract'
sourceCompatibility = '17'

def camelCoreVersion = '3.20.2'
def phConnectorCommonVersion = '1.5.1-gazelle'

repositories {
    mavenCentral()
    maven {
        url = uri("https://mifos.jfrog.io/artifactory/phee-gradle-local")
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation "org.apache.camel.springboot:camel-spring-boot-starter:${camelCoreVersion}"
    implementation 'io.camunda:zeebe-client-java:8.1.9'
    implementation "org.apache.camel.springboot:camel-jackson-starter:${camelCoreVersion}"
    implementation "org.apache.camel:camel-endpointdsl:${camelCoreVersion}"
    implementation "org.apache.camel:camel-undertow:${camelCoreVersion}"
    implementation "org.apache.camel:camel-http:${camelCoreVersion}"
    implementation "org.mifos:ph-ee-connector-common:${phConnectorCommonVersion}"
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.2'
    implementation 'org.json:json:20230227'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'junit:junit:4.11'
    testImplementation 'org.mockito:mockito-core:3.12.4'
    testImplementation "org.apache.camel:camel-test-spring-junit5:${camelCoreVersion}"
    testImplementation "org.apache.camel:camel-mock:${camelCoreVersion}"
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'org.hamcrest:hamcrest'


    // compileOnly dependencies are artifact that are required only during the compilation and not when the application is running. e.g Development tools
    compileOnly(
            'org.projectlombok:lombok'
            )
    annotationProcessor(
            'org.springframework.boot:spring-boot-configuration-processor',
            'org.projectlombok:lombok',
            )
}

allprojects {
    apply plugin: 'com.diffplug.spotless'

    // Configuration for the spotless plugin
    // https://github.com/diffplug/spotless/tree/main/plugin-gradle
    spotless {
        format 'misc', {
            target '**/*.md', '**/*.properties', '**/.gitignore', '**/.openapi-generator-ignore', '**/*.yml', '**/*.xml', '**/**.json', '**/*.sql'
            targetExclude '**/build/**', '**/bin/**', '**/.settings/**', '**/.idea/**', '**/.gradle/**', '**/gradlew.bat'
            indentWithSpaces(4)
            endWithNewline()
            trimTrailingWhitespace()
        }

        groovyGradle {
            target '*.gradle', '**/*.gradle'
            targetExclude '**/build/**'
            greclipse()
            indentWithSpaces(4)
            endWithNewline()
            trimTrailingWhitespace()
        }
        java {
            targetExclude '**/build/**', '**/bin/**', '**/out/**'
            importOrder() //sort imports alphabetically
            removeUnusedImports()
            eclipse().configFile "$rootDir/config/dev-formatter.xml"
            endWithNewline()
            trimTrailingWhitespace()

            // Enforce style modifier order
            custom 'Modifier ordering', {
                def modifierRanking = [
                    public      : 1,
                    protected   : 2,
                    private     : 3,
                    abstract    : 4,
                    default     : 5,
                    static      : 6,
                    final       : 7,
                    transient   : 8,
                    volatile    : 9,
                    synchronized: 10,
                    native      : 11,
                    strictfp    : 12]
                // Find any instance of multiple modifiers. Lead with a non-word character to avoid
                // accidental matching against for instance, "an alternative default value"
                it.replaceAll(/\W(?:public |protected |private |abstract |default |static |final |transient |volatile |synchronized |native |strictfp ){2,}/, {
                    // Do not replace the leading non-word character.  Identify the modifiers
                    it.replaceAll(/(?:public |protected |private |abstract |default |static |final |transient |volatile |synchronized |native |strictfp ){2,}/, {
                        // Sort the modifiers according to the ranking above
                        it.split().sort({ modifierRanking[it] }).join(' ') + ' '
                    }
                    )
                }
                )
            }
        }

        lineEndings 'UNIX'
    }

    compileJava.dependsOn spotlessCheck
}
jacocoTestReport {
    dependsOn test // Ensures tests are run before generating the report

    reports {
        xml.required = true // Enable XML report generation
        html.required = true // Enable HTML report generation
        xml.outputLocation = file("${buildDir}/jacoco/testCoverage.xml") // XML output location
        html.outputLocation = file("${buildDir}/reports/jacoco/test") // HTML output location
    }
}


tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
